# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming version
# made upon the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=19.12.3
pkgrel=1.1
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-415698.patch::"https://cgit.kde.org/dolphin.git/patch/?id=d0cbcf9718bb5192738ea9f5a5a3d6c4f9f7dcef"
kdebug-417640.patch::"https://cgit.kde.org/dolphin.git/patch/?id=4c923a531f77bc4c44b5c8687fe18dd6438eb9c5"
port-deprecated-signal.patch::"https://cgit.kde.org/dolphin.git/patch/?id=e545efee73a869aef4276baef0535169f03933de"
)
sha256sums=('ba16f4d5be5ccc3c135a913f2e3c7dd3b7a492cfc9ec9ae518f714fcd7c2ab47'
            'SKIP'
            'd8b6faaa50c7565e119ead866941f2a87704e28291d7bcd32c2f3f5fd79f81a9'
            'ae1895baecce08de057bf70d9593ae264ccc779ddf8ca1aa7bc4415c21570086'
            'fc0f361efac9b9d0dd9bfd93a3819115b5fe21c654093571636e0b2f450faacb')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply kdebug 415698 patch; fix in 20.04"
  patch -p1 -i ../kdebug-415698.patch
  msg "Apply kdebug 417640 patch; fix in 20.04"
  patch -p1 -i ../kdebug-417640.patch
  msg "Apply patch for qt5.15"
  patch -p1 -i ../port-deprecated-signal.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
