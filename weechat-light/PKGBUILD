# Maintainer : Damian N. <nycko123 at gmail>
# Maintainer : Jacek Bienias <sp7ezd at gmail.com>
# Contributor : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat-light
replaces=('weechat-python3-light')
_pkgname=weechat
pkgver=4.0.0
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI) (polaur)"
conflicts=('weechat')
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'lua' 'perl' 'python' 'ruby' 'tcl')
optdepends=('lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for perl scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("${pkgname}-${pkgver}.tar.xz::https://www.weechat.org/files/src/${_pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.weechat.org/files/src/${_pkgname}-${pkgver}.tar.xz.asc")
sha512sums=('3872ae557943433a62de13ffa549b86554d5fb40a469a172d88beb201429629a2106519812400b6ada12a1cff4a1a549a330e78cb46f958299b7524cd955a4d8'
            'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS} "

build() {
  cmake -B build -S ${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWEECHAT_HOME=~/.config/weechat \
    -DENABLE_MAN=ON \
    -DENABLE_DOC=OFF \
    -DENABLE_GUILE=OFF \
    -DENABLE_ASPELL=OFF \
    -DENABLE_ENCHANT=OFF \
    -DENABLE_PHP=OFF \
    -DENABLE_JAVASCRIPT=OFF \
    -DENABLE_SPELL=OFF \
    -DENABLE_PERL=ON \
    -DENABLSE_PYTHON2=OFF \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
