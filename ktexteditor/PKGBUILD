# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for packages with patches from upcoming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.44.0
pkgrel=1.1
pkgdesc='Advanced embeddable text editor'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kparts' 'syntax-highlighting' 'libgit2' 'editorconfig-core-c')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-391838.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=e75bde233b9fe6b1a7d24cc63fa9baf9e89de895")
sha256sums=('96a50ff4433f1a1060cf81d9538305d546356440df77bf59dbd3ddd1952e1bbc'
            'SKIP'
            '71c0fb553bd3437416d41429fd438865cf734d12486bfdec1a53b2c9e20aaa7d')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 391838 patch" # See: https://bugs.kde.org/show_bug.cgi?id=391838 Fixed in 5.45
  patch -p1 -i ../kdebug-391838.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
