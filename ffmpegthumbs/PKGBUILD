# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ffmpegthumbs
pkgver=20.08.3
pkgrel=1.1
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://www.kde.org/applications/multimedia/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-multimedia)
depends=(kio ffmpeg)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-408956.patch::"https://invent.kde.org/multimedia/ffmpegthumbs/commit/d2ab28b9a4bcc8270ae0e0995710384eec4d46dc.patch")
sha256sums=('f179cc304eeb513444fe0079bddb34ed18cb2ef1125b6294e87de44412bed0a0'
            'SKIP'
            '9636c6ee901bdd1cb401ef4774d8f436c00ac46e1b1b8bfecf709d43b8406a36')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 408956 patch; fix in 20.12.0"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-408956.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
