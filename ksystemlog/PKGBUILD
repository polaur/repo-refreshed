# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksystemlog
pkgver=20.08.3
pkgrel=1.1
pkgdesc='System log viewer tool'
url='https://kde.org/applications/system/ksystemlog/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-system)
depends=(kio)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
fix-memory-leak.patch::"https://github.com/KDE/ksystemlog/commit/82612c9d11d098a7f112bf267756f84f0788edcb.patch")
sha256sums=('2285996b8b1447b94d5d4011a4f9e9d117702f8c4fe873fb5d914d486c15cc85'
            'SKIP'
            'a5ceca60d55687c1dc0dbc11701903168aac700ac8ab8885a8ce05f7e5db8d04')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Fix memory leak"
    patch -d $pkgname-$pkgver -p1 -i ../fix-memory-leak.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
