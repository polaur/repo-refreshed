# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon the work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwindowsystem
pkgver=5.44.0
pkgrel=1.1
pkgdesc='Access to the windowing system'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-x11extras')
makedepends=('extra-cmake-modules' 'qt5-tools' 'doxygen')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
D10669.diff
kdebug-391960.patch::"https://cgit.kde.org/kwindowsystem.git/patch/?id=cc5d6fde1abad968a6c54e872a56833db876dca4")
sha256sums=('1e6f7b6298c244b4c2e0b6c3224ef7a6da2391a2144a53d63e3906ec0113c062'
            'SKIP'
            'ef55bfb2e97a31994763d2d2f2580de55b37be3fc5c7c217a13ab600d2dde01f'
            '14c020f72eafa2fc4070fef596fe0c07d552d8471dcd9da7e734cca0191cefcc')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  msg "You should have install plasma-workspace 5.12.3-1.1 too, add D10669 patch" # See: https://phabricator.kde.org/file/data/j2oywrfvsrwfgvorc442/PHID-FILE-eyy2rtwp5ess5vi646mw/D10669.diff
  patch -p1 -i ../D10669.diff
  msg "Add KDEBUG 391960 patch" # See: https://bugs.kde.org/show_bug.cgi?id=391960
  patch -p1 -i ../kdebug-391960.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
