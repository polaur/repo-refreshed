# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=powerdevil
pkgver=5.13.1
pkgrel=1.1
pkgdesc='Manages the power consumption settings of a Plasma Shell'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace bluez-qt networkmanager-qt libkscreen)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-387284.patch::"https://cgit.kde.org/powerdevil.git/patch/?id=4b8ae4d708a6f4cc854ea82ca96987f0b47ea722")
sha256sums=('8d4edcf16ece7d8a1a961e6fa052d731adb699b24900d54d38534d74cc5b1396'
            'SKIP'
            'f0980869c581208fd089cf2c6208c20324e20a432e37d53311f58763a24cb1ae')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 387284 patch" # Fixed in 5.14.0
  patch -p1 -i ../kdebug-387284.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
