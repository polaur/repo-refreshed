# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made on the work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace kuiserver plasma-wayland-session)
pkgver=5.12.2
pkgrel=1.2
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kjsembed knotifyconfig libksysguard ktexteditor libqalculate qt5-tools kde-cli-tools bc
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage xorg-xprop milou prison kwin
         plasma-integration kdelibs4support)
makedepends=(extra-cmake-modules kdoctools gpsd baloo appstream-qt krunner networkmanager-qt kdesignerplugin)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
ccbug-358231-342056.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=e1fe26f1fc78"
kdebug-375207.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=8227e4e2b7f0"
kdebug-390499.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=53ed39aaa477"
D10692.diff)
sha256sums=('2d7e45761730c7eecbb91a6951c9ae3a4d08dc0a5a80fec640cc1cbfe2569dec'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '1acfd90910c110e0dccba413831b7493a0a08a9e75c857212c8741149d4a4c96'
            '08745bd6c66e51937dd7c28ce7846dca6fe9335b237af4e3b8f4bdeac0bc5beb'
            '2546c42475d9c62a5efbd6c709f52062194df233c098dbee1ce653f2de9376ce'
            'f3f56921ec3b801e9856f496af657b9f368a31675abb2da9bf684107832c1ce4')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add CCBUG 358231 & CCBUG 342056 patch" # See: https://cgit.kde.org/plasma-workspace.git/commit/?id=8227e4e2b7f034166cbf5efb01e4e715028b4606
  patch -p1 -i ../ccbug-358231-342056.patch
  msg "Add kdebug-375207 patch" # See: https://bugs.kde.org/show_bug.cgi?id=375207 Fixed in 5.13.0
  patch -p1 -i ../kdebug-375207.patch
  msg "Add KDEBUG 390499 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390499
  patch -p1 -i ../kdebug-390499.patch
  msg "Add D10692 patch" # See: https://phabricator.kde.org/D10692
  patch -p1 -i ../D10692.diff
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  depends+=(kuiserver)
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-qt4: global menu support for Qt4 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner'
              'appstream-qt: package search runner')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Remove conflicts with kuiserver
  rm "$pkgdir"/usr/bin/kuiserver5
  rm "$pkgdir"/usr/lib/libkdeinit5_kuiserver5.so
  rm "$pkgdir"/usr/share/dbus-1/services/kf5_org.kde.kuiserver.service
  rm "$pkgdir"/usr/share/kservices5/kuiserver.desktop
  rm "$pkgdir"/etc/xdg/kuiserver.categories

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_kuiserver() {
  pkgdesc='KDE Progress Info UI server'
  depends=(kxmlgui)

  cd build/kuiserver
  make DESTDIR="$pkgdir" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
