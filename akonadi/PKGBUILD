# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=20.04.1
pkgrel=1.1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kitemmodels kaccounts-integration)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-408897.patch::"https://cgit.kde.org/akonadi.git/patch/?id=b94b8115f9586033983b059ae1faa731ef3defb1"
)
sha256sums=('0bffbcd00e0d4f4f3a2084ce6a42da8f05fdf14cb361141b4b734e35203d764e'
            'SKIP'
            '6666cd2630e88dceda20e9c4e6a8aab8d55c100214c1930e7fcec601889669f0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-408897.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends=(kitemmodels kaccounts-integration hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends=(libakonadi mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
