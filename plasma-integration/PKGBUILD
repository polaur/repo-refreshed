# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=plasma-integration
pkgver=5.13.2
pkgrel=1.1
pkgdesc="Qt Platform Theme integration plugins for the Plasma workspaces"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kio kwayland libxcursor noto-fonts ttf-hack qqc2-desktop-style)
makedepends=(extra-cmake-modules breeze)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
ccbug-378523.patch::"https://cgit.kde.org/plasma-integration.git/patch/?id=2e971be576d24a82974eaf2397d4703fca0188d8"
kdebug-376340+379719.patch::"https://cgit.kde.org/plasma-integration.git/patch/?id=73eeda3a7dfb3b155a6198ff733e5ab2f1a89f0e")
sha256sums=('bd42d79d5a4aab4618aa113d9f577654473624a46d04de375bf330d94028e308'
            'SKIP'
            '285784bbda194ed136cb727f614b5ede0c00d0ce5bb385ff9cd032a114742529'
            '41532f5db87c44c64b73a0bc9ad12ed37da8d7b7d8e8e880568ed8ac24ace806')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add CCBUG 378523 patch"
  patch -p1 -i ../ccbug-378523.patch
  msg "Add KDEBUG 376340 & 379719 patch"
  patch -p1 -i ../kdebug-376340+379719.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
